apply plugin: 'com.android.application'
apply from: '../jacoco.gradle'

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        applicationId appApplicationId
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 1
        versionName "1"
        testInstrumentationRunner appTestRunner
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        if (System.env.TRAVIS == 'true') {
            preDexLibraries = false
        }
    }
}

dependencies {
    compile project(':ibeaconscanner') // library to scan for iBeacons
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'io.reactivex:rxjava:1.2.0' // rxjava
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar' // library to handle location permission
    compile 'com.facebook.stetho:stetho:1.4.1' // stetho to easily see our database
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5' // leak canary to detect leaks
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5' // leak canary to detect leaks
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    androidTestCompile 'com.android.support:support-annotations:25.0.1'
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0' // for mockito
    androidTestCompile 'com.google.dexmaker:dexmaker:1.0' // for mockito
    androidTestCompile 'org.mockito:mockito-core:1.10.17' // for mockito

    testCompile 'junit:junit:4.12'
}

sonarqube {
    properties {
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.jacoco.reportPath", "build/jacoco/testDebugUnitTest.exec" // junit coverage report
        property "sonar.junit.reportsPath", "build/combined/" // path to UI + junit reports
        property "sonar.android.lint.report", "build/outputs/lint-results-debug.xml" // path to lint report
    }
}
